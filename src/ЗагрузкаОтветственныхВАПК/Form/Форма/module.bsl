
Процедура КнопкаВыполнитьНажатие(Кнопка)
	
	Если ЗначениеЗаполнено( Конфигурация )
		И Ответственные.Количество() > 0 Тогда
		
		ЗаполнитьОтветственных( Конфигурация );
		
	КонецЕсли;
	
КонецПроцедуры

Процедура КоманднаяПанель1ПрочитатьФайл(Кнопка)
	
	ПрочитатьФайл();
	
КонецПроцедуры

Процедура ПрочитатьФайл()
	
	тз = ЗначениеИзФайла( ПутьКФайлу );
	
	Ответственные.Загрузить( тз );
	
	Ответственные.Сортировать( "Ответственный,ПутьОбъекта" );

КонецПроцедуры

Процедура ЗаполнитьОтветственных( Знач пКонфигурация )
	
	ТаблицаОтветственных = ПолучитьТаблицуОтветственных();
	
	Версия = НайтиПоследнююВерсию(пКонфигурация, Ложь);
	Если Версия.Пустая() Тогда
		Сообщить(НСтр("ru='Для указанной конфигурации не существует ни одной версии.'"));
		Возврат;
	КонецЕсли;
	
	ТаблицаОбъектов = ПолучитьТаблицуОбъектовКонфигурации(Версия);
	Если ТаблицаОбъектов.Количество() = 0 Тогда
		Сообщить(НСтр("ru='Для указанной конфигурации не собрана структура метаданных.'"));
		Возврат;
	КонецЕсли;
	
	НазначениеОтветственных = Обработки.НазначениеОтветственных.Создать();
	НазначениеОтветственных.Версия = Версия;
	
	ТекстСостоянияШаблон = НСтр("ru='Выполняется загрузка ответственных объектов метаданных. Обрабатывается объект №%1 из %2'");
	
	всегоСтрок = Ответственные.Количество();
	
	Для каждого цСтрока Из Ответственные Цикл
		
		ИзменитьОтветственного( цСтрока, НазначениеОтветственных, ТаблицаОбъектов, ТаблицаОтветственных, всегоСтрок);
		
		
	КонецЦикла;
	
	Сообщить(НСтр("ru='Загрузка ответственных завершена.'"));
	
КонецПроцедуры

Процедура ИзменитьОтветственного(цСтрока, Знач НазначениеОтветственных, Знач ТаблицаОбъектов, ТаблицаОтветственных, Знач всегоСтрок)
	
	префикс = "" + цСтрока.НомерСтроки + "/" + всегоСтрок + ". ";
	
	#Если Клиент Тогда
		ОбработкаПрерыванияПользователя();
	#КонецЕсли
	
	ОтветственныйИмя = цСтрока.Ответственный;
	ОтветственныйИмя = ВРег(СтрЗаменить(ОтветственныйИмя, " ", ""));
	
	// Ищем ответственного в таблице пользователей.
	ОтветственныйСтрока = ТаблицаОтветственных.Найти(ОтветственныйИмя);
	Если ОтветственныйСтрока = Неопределено Тогда
		
		новПользователь = Справочники.Пользователи.СоздатьЭлемент();
		новПользователь.Наименование = цСтрока.Ответственный;
		новПользователь.Записать();
		
		ТаблицаОтветственных = ПолучитьТаблицуОтветственных();
		
		ОтветственныйСтрока = ТаблицаОтветственных.Найти(ОтветственныйИмя);
		
	КонецЕсли;
	
	Если ОтветственныйСтрока = Неопределено Тогда
		ТекстСообщения =
		НСтр("ru='В справочнике ""Пользователи"" не найден пользователь ""%1"" из строки №%2 ответственных.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, цСтрока.Ответственный, цСтрока.НомерСтроки);
		Сообщить( префикс + ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	НовыйОтветственный = ОтветственныйСтрока.Ссылка;
	
	ПутьОбъекта = цСтрока.ПутьОбъекта;
	ПутьОбъекта = ВРег(СтрЗаменить(ПутьОбъекта, " ", ""));
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ПутьОбъектаВРег", ПутьОбъекта);
	
	// Ищем объект в таблице объектов по пути.
	ТаблицаОбъектовОтбор = ТаблицаОбъектов.Скопировать(СтруктураОтбора);
	Если ТаблицаОбъектовОтбор.Количество() = 0 Тогда
		// Не нашли, добавим в начало "Общие." и снова поищем.
		Если НЕ СтрНачинаетсяС(ПутьОбъекта, ВРег("Общие.")) Тогда
			ПутьОбъекта = ВРег("Общие.") + ПутьОбъекта;
			СтруктураОтбора.Вставить("ПутьОбъектаВРег", ПутьОбъекта);
			ТаблицаОбъектовОтбор = ТаблицаОбъектов.Скопировать(СтруктураОтбора);
		КонецЕсли;
	КонецЕсли;
	
	// Не нашли, поищем по наименованию объекта.
	Если ТаблицаОбъектовОтбор.Количество() = 0 Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НаименованиеВРег", ПутьОбъекта);
		
		ТаблицаОбъектовОтбор = ТаблицаОбъектов.Скопировать(СтруктураОтбора);
	КонецЕсли;
	
	// Не нашли, сообщим, что не нашли объект.
	Если ТаблицаОбъектовОтбор.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru='В структуре конфигурации не найден объект ""%1"" из строки №%2 файла ответственных.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, цСтрока.ПутьОбъекта, цСтрока.НомерСтроки);
		Сообщить( префикс + ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	Если ТаблицаОбъектовОтбор.Количество() > 1 Тогда
		ТекстСообщения = НСтр("ru='В структуре конфигурации найдено несколько объектов ""%1"",
		|подходящих под описание из строки №%2 файла ответственных. Ответственный будет проставлен всем.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, цСтрока.ПутьОбъекта, цСтрока.НомерСтроки);
		Сообщить( префикс + ТекстСообщения);
		
	КонецЕсли;
	
	Для каждого цНайденнаяСтрока Из ТаблицаОбъектовОтбор Цикл
		
		ОбъектСсылка = цНайденнаяСтрока.Ссылка;
		СтарыйОтветственный = цНайденнаяСтрока.Ответственный;
		
		Если Не НовыйОтветственный = СтарыйОтветственный Тогда
			
			НазначениеОтветственных.ИзменитьОтветственного(ОбъектСсылка, НовыйОтветственный, СтарыйОтветственный,, Ложь);
			
			Сообщить( префикс + цСтрока.ПутьОбъекта + "->" + НовыйОтветственный);
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ПутьКФайлуНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗаголовокДиалога = НСтр("ru='Выберите файл с ответственными'");
	Фильтр = "Текстовый файл (*.txt)|*.txt|Все файлы (*.*)|*.*";
	
	ПутьКФайлу = ИмпортЭкспортКлиент.ВыборФайла( ПутьКФайлу, Ложь, ЗаголовокДиалога, Фильтр);
	
	Если ЗначениеЗаполнено( ПутьКФайлу ) Тогда
		
		ПрочитатьФайл();
		
	КонецЕсли;
	
КонецПроцедуры
